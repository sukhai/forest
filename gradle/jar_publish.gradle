apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

jar {
    manifest {
        attributes "Implementation-Title": "${rootProject.name}${project.name}",
            "Implementation-Version": project.version
    }
    shouldRunAfter test
}

task install {
    group = 'publishing'
    description = 'Build and publish to maven local'
}
install.finalizedBy(build, publishToMavenLocal)

task sourcesJar(type: Jar) {
    group = 'publishing'
    description = 'Create a jar type archive file for all the source files'

    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    group = 'publishing'
    description = 'Create'

    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

publishing {
    publications {
        artifact(MavenPublication) {
            groupId "$project.group"
            artifactId "$project.name"
            version "$project.version"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Forest'
                description = 'A library that log messages.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
}