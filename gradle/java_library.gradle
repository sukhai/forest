apply plugin: 'java-library'
apply plugin: 'kotlin'
apply from: "$rootDir/gradle/ktlint.gradle"
apply from: "$rootDir/gradle/dokka.gradle"
apply from: "$rootDir/gradle/java_jacoco.gradle"
apply plugin: 'maven-publish'

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Implementation-Title": project.name,
            "Implementation-Version": project.version
    }
    shouldRunAfter test
}

task install() {
    group = 'publishing'
    description = 'Build and publish to maven local'
}
install.finalizedBy(build, publishToMavenLocal)

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

publishing {
    publications {
        artifact(MavenPublication) {
            groupId "${project.group}"
            artifactId "${project.name}"
            version "${project.version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Forest'
                description = 'A library that log messages.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:${versions.junit}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${versions.mockito}"
}