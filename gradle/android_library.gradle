apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$rootDir/gradle/ktlint.gradle"
apply from: "$rootDir/gradle/dokka.gradle"
apply from: "$rootDir/gradle/android_jacoco.gradle"
apply plugin: 'maven-publish'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion versions.sdk_compile

    defaultConfig {
        minSdkVersion versions.sdk_min
        targetSdkVersion versions.sdk_target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

task install {
    group = 'publishing'
    description = 'Build and publish to maven local'
}
install.finalizedBy(assemble, publishToMavenLocal)

task sourcesJar(type: Jar) {
    group = 'publishing'
    description = 'Create sources jar'

    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId "$project.group"
                artifactId "$project.name"
                version "$project.version"

                from components.release
                artifact sourcesJar

                pom {
                    name = 'Forest'
                    description = 'A library that log messages.'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:${versions.junit}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${versions.mockito}"

    androidTestImplementation "org.junit.jupiter:junit-jupiter:${versions.junit}"
    androidTestImplementation "androidx.test.ext:junit:${versions.android_junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.android_espresso}"
    androidTestUtil "androidx.test:orchestrator:${versions.android_orchestrator}"
}